plugins {
    // gradle shadowJar ==>  build/libs/spot-wx-1.0-all.jar
    id 'com.gradleup.shadow' version '8.3.3'
    id 'java'
    id 'application'
}

mainClassName = 'com.srednal.spotwx.SpotWx'
sourceCompatibility = 16
targetCompatibility = 16
version = '1.0'

def gitLocal = 'git diff --quiet --exit-code'.execute().waitFor()
def gitHash = 'git rev-parse --short HEAD'.execute().text.trim()
def implVersion = gitLocal == 0 ? gitHash : 'LOCAL'

jar {
    // to check the manifest impl version:
    // unzip -p build/libs/spot-wx-1.0-all.jar META-INF/MANIFEST.MF
    manifest {
        attributes 'Multi-Release': 'true' // required for log4j
        attributes 'Implementation-Version': implVersion
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.api-client:google-api-client:2.0.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.apis:google-api-services-gmail:v1-rev20220404-2.0.0'
    implementation 'javax.mail:mail:1.4.7'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2'

    implementation 'net.iakovlev:timeshape:2024a.24'
    implementation 'org.slf4j:slf4j-nop:2.0.16' // timeshape uses slf4j, don't log

    implementation platform('org.apache.logging.log4j:log4j-bom:2.24.1')
    implementation 'org.apache.logging.log4j:log4j-api:${log4j-api.version}'
    runtimeOnly 'org.apache.logging.log4j:log4j-core'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
}

test {
    useJUnitPlatform()
}

tasks.register('plutil', Exec) {
    commandLine '/usr/bin/plutil', 'com.srednal.spotwx.plist'
}
tasks.build.dependsOn(plutil)
